dnl CUPS stuffs (to be changed)
ac_use_cups="yes"
CUPSSUBDIR=
LIB_CUPS=
AC_ARG_ENABLE(cups,[  --disable-cups          disable CUPS support [default=enabled]],[ac_use_cups="no"])
if test "$ac_use_cups" = "yes"; then
  ac_cups_libs=0
  ac_CPPFLAGS_save="$CPPFLAGS"
  CPPFLAGS="$CPPFLAGS $all_includes"
  ac_LDFLAGS_save="$LDFLAGS"
  LDFLAGS="$LDFLAGS $all_libraries"
  AC_CHECK_LIB(cups,cupsServer,[ac_cups_libs=1],[ac_cups_libs=0],$LIBDL)
  if test "$ac_cups_libs" = "0"; then
    AC_MSG_WARN(CUPS library not found. CUPS support disabled)
    ac_use_cups="no"
  fi
  ac_cups_headers=0
  AC_CHECK_HEADER(cups/cups.h,[ac_cups_headers=1])
  if test "$ac_cups_headers" = "0"; then
    AC_MSG_WARN(CUPS headers not found. CUPS support disabled)
    ac_use_cups="no"
  fi
  LDFLAGS="$ac_LDFLAGS_save"
  CPPFLAGS="$ac_CPPFLAGS_save"
fi
if test "$ac_use_cups" = "yes"; then
  CUPSSUBDIR="cups"
  LIB_CUPS="-lcups"
  AC_DEFINE(HAVE_CUPS, 1, [Defines if you have CUPS (Common UNIX Printing System)])
fi
AC_SUBST(CUPSSUBDIR)
AC_SUBST(LIB_CUPS)
